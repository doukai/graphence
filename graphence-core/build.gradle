buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.9.1'
    id 'maven-publish'
    id "org.graphoenix" version "0.1.1"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.7"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.52.1'
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:1.2.3"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            reactor {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/reactor'
        }
    }
}

group 'org.graphoenix'
version '0.1.2'

repositories {
    mavenCentral()
    jcenter()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

sourcesJar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

dependencies {
    api 'org.eclipse.microprofile.jwt:microprofile-jwt-auth-api:2.1'
    api 'org.casbin:jcasbin:1.41.0'
    api 'io.jsonwebtoken:jjwt-api:0.11.5'
    api 'com.password4j:password4j:1.7.2'

    implementation 'org.graphoenix:graphoenix-core:0.1.2'
    implementation 'org.graphoenix:graphoenix-http-server:0.1.3'
    implementation 'org.graphoenix:graphoenix-structure:0.1.2'
    implementation 'org.graphoenix:nozdormu-inject:0.1.0'
    implementation 'org.graphoenix:nozdormu-async:0.1.0'
    implementation 'org.graphoenix:nozdormu-interceptor:0.1.0'
    implementation 'org.graphoenix:nozdormu-config:0.1.0'
    implementation 'com.aventrix.jnanoid:jnanoid:2.0.0'
    implementation 'com.google.guava:guava:31.1-jre'

    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    runtimeOnly 'io.grpc:grpc-netty-shaded:1.52.1'
    implementation 'io.grpc:grpc-protobuf:1.52.1'
    implementation 'io.grpc:grpc-stub:1.52.1'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.3'

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    annotationProcessor 'org.graphoenix:graphoenix-annotation-processor:0.1.2'
    annotationProcessor 'org.graphoenix:graphoenix-http-server:0.1.3'
    annotationProcessor 'org.graphoenix:graphoenix-introspection:0.1.2'
    annotationProcessor 'org.graphoenix:graphoenix-structure:0.1.2'
    annotationProcessor 'org.graphoenix:nozdormu-inject:0.1.0'
    annotationProcessor 'org.graphoenix:nozdormu-async:0.1.0'
    annotationProcessor 'org.graphoenix:nozdormu-interceptor:0.1.0'
    annotationProcessor 'org.graphoenix:nozdormu-config:0.1.0'
    annotationProcessor 'org.casbin:jcasbin:1.33.1'

    protobuf 'org.graphoenix:graphoenix-core:0.1.2'
    protobuf 'org.graphoenix:graphoenix-introspection:0.1.2'
    protobuf 'org.graphoenix:graphoenix-structure:0.1.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.graphoenix'
            artifactId = 'graphence-core'

            from components.java

            pom {
                name = 'graphence-core'
                description = 'graphence-core'
                url = 'https://github.com/doukai/graphence'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'doukai'
                        name = 'Dou Kai'
                    }
                }
                scm {
                    url = 'https://github.com/doukai/graphence'
                    connection = 'scm:git://github.com:doukai/graphence.git'
                    developerConnection = 'scm:git://github.com:doukai/graphence.git'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

tasks.build.doFirst {
    mkdir "$buildDir/jreleaser"
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}